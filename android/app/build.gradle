apply plugin: 'com.android.application'
def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "org.coolreader.mod.plotn"
        minSdkVersion 14
        targetSdkVersion 28
        multiDexEnabled true
        // When new version released, version code must be incremented at least by 8
        // for compatibility with ABI versioning of split apk (see below).
        versionCode 32390
        versionName "3.2.39-1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_static"
                cppFlags ""
            }
        }
        buildConfigField("String", "YND_ID", apikeyProperties['YND_ID'])
        buildConfigField("String", "YND_PASSW", apikeyProperties['YND_PASSW'])
        buildConfigField("String", "DBX_KEY", apikeyProperties['DBX_KEY'])
        buildConfigField("String", "DBX_SECRET", apikeyProperties['DBX_SECRET'])
        buildConfigField("String", "YND_TRANSLATE", apikeyProperties['YND_TRANSLATE'])
        buildConfigField("String", "LINGVO", apikeyProperties['LINGVO'])
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
    }

    sourceSets {
        main {
            setRoot('src/main')
            aidl.srcDirs = ['../../android/src']
            java.srcDirs = ['../../android/src']
            res.srcDirs = ['../../android/res']
            jni.srcDirs = ['../../android/jni']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            externalNativeBuild {
                cmake {
                    arguments "-DCMAKE_BUILD_TYPE=RelWithDebInfo"
                }
            }
            packagingOptions{
                doNotStrip "**/*.so"
            }
        }
        release_minsize {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules-optimize.pro'
            externalNativeBuild {
                cmake {
                    arguments "-DCMAKE_BUILD_TYPE=Release"
                }
            }
        }
        debug {
            minifyEnabled false
            shrinkResources false
            jniDebuggable true
            debuggable true
            externalNativeBuild {
                cmake {
                    arguments "-DCMAKE_BUILD_TYPE=Debug"
                }
            }
            packagingOptions{
                doNotStrip "**/*.so"
            }
        }
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked"
        }
    }
    // https://developer.android.com/studio/build/configure-apk-splits
    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a' //select ABIs to build APKs for
            universalApk true //generate an additional APK that contains all the ABIs
        }
    }
     // map for the version code
    project.ext.abiCodes = [
            'armeabi': 1,
            'armeabi-v7a': 2,
            'arm64-v8a': 6,
            'mips': 3,
            'mips64': 7,
            'x86': 4,
            'x86_64': 5
    ]
    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            // Stores the value of ext.abiCodes that is associated with the ABI for this variant.
            def baseAbiVersionCode = project.ext.abiCodes.get(output.getFilter(com.android.build.OutputFile.ABI))
            if (baseAbiVersionCode != null) {
                // Variant 1: small version code increment for various ABI (old behaviour)
                output.versionCodeOverride = variant.versionCode + baseAbiVersionCode
                // Variant 2: big version code increment for various ABI
                //output.versionCodeOverride = variant.versionCode + 1000000*baseAbiVersionCode
            }
            def project = "CoolReader_plotn"
            def SEP = "_"
            def buildType = variant.variantData.variantConfiguration.buildType.name
            def version = variant.versionName
            def date = new Date();
            def formattedDate = date.format('yyyyMMdd_HHmm')
            def abi = output.getFilter(com.android.build.OutputFile.ABI)
            if (abi == null) abi = "universal"
            def newApkName = formattedDate + SEP + project + SEP + version + SEP + abi + SEP + buildType + ".apk"
            output.outputFileName = newApkName
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    lintOptions {
        disable 'MissingTranslation'
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    useLibrary 'org.apache.http.legacy'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation files('libs/LingvoIntegration_2.5.2.12.jar')
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.google.android.gms:play-services-identity:15.0.1'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.github.deano2390:FlowTextView:2.0.5'
    implementation 'com.dropbox.core:dropbox-core-sdk:3.0.11'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.9'
    implementation files('libs/okhttp-3.12.3.jar')
    implementation files('libs/okio-1.15.0.jar')
    //api 'com.squareup.okhttp3:okhttp:4.0.1'
    testImplementation 'junit:junit:4.12'
    //implementation 'net.htmlparser.jericho:jericho-html:3.4'
    implementation 'org.jsoup:jsoup:1.12.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation files('libs/commons.jar')
    implementation files('libs/OpenDocument.jar')
    //implementation 'org.jopendocument:jOpenDocument:1.3'
}
